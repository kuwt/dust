!./\\\\\\\\\\\...../\\\......./\\\..../\\\\\\\\\..../\\\\\\\\\\\\\.
!.\/\\\///////\\\..\/\\\......\/\\\../\\\///////\\\.\//////\\\////..
!..\/\\\.....\//\\\.\/\\\......\/\\\.\//\\\....\///.......\/\\\......
!...\/\\\......\/\\\.\/\\\......\/\\\..\////\\.............\/\\\......
!....\/\\\......\/\\\.\/\\\......\/\\\.....\///\\...........\/\\\......
!.....\/\\\......\/\\\.\/\\\......\/\\\.......\///\\\........\/\\\......
!......\/\\\....../\\\..\//\\\...../\\\../\\\....\//\\\.......\/\\\......
!.......\/\\\\\\\\\\\/....\///\\\\\\\\/..\///\\\\\\\\\/........\/\\\......
!........\///////////........\////////......\/////////..........\///.......
!!=========================================================================
!!
!! Copyright (C) 2018-2022 Politecnico di Milano,
!!                           with support from A^3 from Airbus
!!                    and  Davide   Montagnani,
!!                         Matteo   Tugnoli,
!!                         Federico Fonte
!!
!! This file is part of DUST, an aerodynamic solver for complex
!! configurations.
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or sell
!! copies of the Software, and to permit persons to whom the
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!!
!! Authors:
!!          Federico Fonte
!!          Davide Montagnani
!!          Matteo Tugnoli
!!=========================================================================

!> Module containing the subroutines to perform visualizations during
!! postprocessing
module mod_post_viz

use mod_param, only: &
  wp, nl, max_char_len, extended_char_len , pi

use mod_handling, only: &
  error, warning, info, printout, dust_time, t_realtime, new_file_unit

use mod_parse, only: &
  t_parse, &
  getstr, getlogical, &
  countoption

use mod_hdf5_io, only: &
  h5loc, &
  open_hdf5_file, &
  close_hdf5_file, & 
  open_hdf5_group, &
  close_hdf5_group, &
  read_hdf5

use mod_stringtools, only: &
  LowCase, isInList

use mod_geometry, only: &
  t_geo, t_geo_component, destroy_elements

use mod_geo_postpro, only: &
  load_components_postpro, update_points_postpro, prepare_geometry_postpro, &
  expand_actdisk_postpro

use mod_tecplot_out, only: &
  tec_out_viz

use mod_vtk_out, only: &
  vtk_out_viz

use mod_post_load, only: &
  load_refs, load_res, load_wake_viz , check_if_components_exist

use mod_vtk_utils, only: &
  t_output_var, add_output_var, copy_output_vars, clear_output_vars

use mod_wind, only: &
  variable_wind

implicit none

public :: post_viz

private

character(len=*), parameter :: this_mod_name='mod_post_viz'
character(len=max_char_len) :: msg

contains

! ----------------------------------------------------------------------

subroutine post_viz( sbprms , basename , data_basename , an_name , ia , &
                      out_frmt , components_names , all_comp , &
                      an_start , an_end , an_step, average )
  type(t_parse), pointer :: sbprms
  character(len=*) , intent(in) :: basename
  character(len=*) , intent(in) :: data_basename
  character(len=*) , intent(in) :: an_name
  integer          , intent(in) :: ia
  character(len=*) , intent(in) :: out_frmt
  character(len=max_char_len), allocatable , intent(inout) :: components_names(:)
  logical , intent(in) :: all_comp
  integer , intent(in) :: an_start , an_end , an_step
  logical, intent(in) :: average

  type(t_geo_component), allocatable :: comps(:)
  character(len=max_char_len) :: filename
  integer(h5loc) :: floc , ploc
  logical :: out_vort, out_vort_vec, out_vel, out_cp, out_press , out_wake, out_surfvel, out_vrad
  logical :: out_turbvisc
  logical :: separate_wake
  integer :: n_var , i_var
  character(len=max_char_len), allocatable :: var_names(:)
  real(wp), allocatable :: points(:,:), points_exp(:,:) , wpoints(:,:)
  real(wp), allocatable :: vppoints(:,:), vpvort(:), vpvort_v(:,:), vpturbvisc(:), v_rad(:)
  integer , allocatable :: elems(:,:) , welems(:,:)
  integer :: nelem , nelem_w, nelem_vp

  real(wp), allocatable :: points_ave(:,:)

  real(wp) :: u_inf(3)
  real(wp) :: P_inf , rho

  real(wp), allocatable :: refs_R(:,:,:), refs_off(:,:)
  real(wp), allocatable :: vort(:), cp(:), vel(:), press(:), surfvel(:,:)
  real(wp), allocatable :: wvort(:)

  type(t_output_var), allocatable :: out_vars(:), ave_out_vars(:)
  type(t_output_var), allocatable :: out_vars_w(:), out_vars_vp(:)
  integer :: nprint , nprint_w, nelem_out

  integer :: it, ires
  real(wp) :: t
  character(len=*), parameter :: this_sub_name='post_viz'

  write(msg,'(A,I0,A)') nl//'++++++++++ Analysis: ',ia,' visualization'//nl
  call printout(trim(msg))

  ! Print the wake or not
  out_wake = getlogical(sbprms,'wake')
  separate_wake = getlogical(sbprms,'separate_wake')

  !Check which variables to analyse
  out_vort = .false.; out_vel = .false.; out_press =.false.; out_cp = .false.
  n_var = countoption(sbprms, 'variable')
  allocate(var_names(n_var))
  do i_var = 1, n_var
    var_names(i_var) = getstr(sbprms, 'variable') ; call LowCase(var_names(i_var))
  enddo
  out_vort = isInList('vorticity',var_names) ! Always lower case string in the code !
  out_vort_vec = isInList('vorticity_vector',var_names) ! Always lower case string in the code !
  out_vel  = isInList('velocity' ,var_names)
  out_surfvel= isInList('surface_velocity' ,var_names)
  out_press= isInList('pressure' ,var_names)
  out_cp   = isInList('cp'       ,var_names)
  out_turbvisc = isInList('turbulent_viscosity',var_names)
  out_vrad = isInList('vortex_rad',var_names)
  nprint = 0; nprint_w = 0
  if(out_vort)  nprint = nprint+1
  if(out_vort_vec)  nprint = nprint+1
  if(out_cp)    nprint = nprint+1
  if(out_surfvel)   nprint = nprint+1
  if(out_vel)   nprint = nprint+1  !<--- *** TODO ***
  if(out_press) nprint = nprint+1  !<--- *** TODO ***
  if(out_turbvisc) nprint = nprint+1
  if(out_vrad) nprint = nprint+1
  allocate(out_vars(nprint))
  if(average) allocate(ave_out_vars(nprint))
  !for the wake
  if(out_wake) then
    allocate(out_vars_w(nprint))
    allocate(out_vars_vp(nprint))
  endif


  ! Load the components (just once)
  call open_hdf5_file(trim(data_basename)//'_geo.h5', floc)


  call load_components_postpro(comps, points, nelem, floc, &
                               components_names, all_comp)

  call close_hdf5_file(floc)


  if(out_wake .and. average) call error(this_sub_name, this_mod_name, &
  'Cannot output an averaged wake visualization. Remove the wake or avoid &
  &averaging')

  ! Prepare_geometry_postpro
  call prepare_geometry_postpro(comps)

  ! Time loop
  ires = 0
  do it = an_start, an_end, an_step

    ires = ires+1

    ! Open the file
    write(filename,'(A,I4.4,A)') trim(data_basename)//'_res_',it,'.h5'
    call open_hdf5_file(trim(filename),floc)

    ! Load free-stream parameters
    call open_hdf5_group(floc,'Parameters',ploc)
    call read_hdf5(u_inf,'u_inf',ploc)
    call read_hdf5(P_inf,'P_inf',ploc)
    call read_hdf5(rho,'rho_inf',ploc)
    call close_hdf5_group(ploc)

    ! Load the references
    call load_refs(floc,refs_R,refs_off)

    ! Move the points
    call update_points_postpro(comps, points, refs_R, refs_off, &
                               filen = trim(filename) )
    !expand the actuator disks
    call expand_actdisk_postpro(comps, points, points_exp, elems)
    if(average) then
      if( .not. allocated(points_ave)) then
        allocate(points_ave(size(points_exp,1),size(points_exp,2)))
        points_ave = 0.0_wp
      endif
      points_ave = points_ave*(real(ires-1,wp)/real(ires,wp)) + &
                   points_exp/real(ires,wp)
    endif

    !Load the results ! TODO: check this routine and the content of the files to be read
    ! TODO : compute the missing quantities
    if(out_surfvel) then
      call load_res(floc, comps, vort, press, t, surfvel)
    else
      call load_res(floc, comps, vort, press, t)
    endif

    !Prepare the variable for output
    nelem_out = size(vort)
    ! TODO: compute/or read pressure and velocity field. Now set equal to zero
    allocate(  vel(size( vort,1)) ) ; vel = 0.0_wp
    allocate(   cp(size(press,1)) ) ;  cp = 0.0_wp
    ! pressure coefficient
    cp = (press - P_inf)/(0.5_wp*rho*norm2(u_inf)**2)

    i_var = 1
    if(out_vort) then
      call add_output_var(out_vars(i_var), vort, 'Singularity_Intensity', &
                          .false.)
      i_var = i_var +1
    endif
    if(out_cp) then
      call add_output_var(out_vars(i_var), cp, 'Cp',.false.)
      i_var = i_var +1
    endif
    if(out_surfvel) then
      call add_output_var(out_vars(i_var), surfvel, 'Surface_Velocity',.false.)
      i_var = i_var +1
    endif
    if(out_vel) then
      call add_output_var(out_vars(i_var), vel, 'Velocity',.false.)
      i_var = i_var +1
    endif
    if(out_press) then
      call add_output_var(out_vars(i_var), press, 'Pressure',.false.)
      i_var = i_var +1
    endif

    
    if(average) then
      if( ires .eq. 1) then
        call copy_output_vars(out_vars, ave_out_vars, .true.)
      endif
      do i_var = 1,size(ave_out_vars)
      ave_out_vars(i_var)%var = ave_out_vars(i_var)%var * &
                                (real(ires-1,wp)/real(ires,wp)) + &
                                out_vars(i_var)%var/real(ires,wp)
      enddo
    endif

    if(.not. average) then
      ! Output filename
      write(filename,'(A,I4.4)') trim(basename)//'_'//trim(an_name)//'-',it

      if (out_wake) then

        if(out_turbvisc) then
          call load_wake_viz(floc, wpoints, welems, wvort, vppoints, vpvort, &
                            vpvort_v, v_rad, vpturbvisc)
        else
          call load_wake_viz(floc, wpoints, welems, wvort, vppoints, vpvort, vpvort_v, v_rad)
        endif
        nelem_w = size(welems,2)
        nelem_vp = size(vppoints,2)

        i_var = 1
        if(out_vort) then
          call add_output_var(out_vars_w(i_var), wvort, &
                  'Singularity_Intensity',.false.)
          call add_output_var(out_vars_vp(i_var), vpvort, &
                  'Singularity_Intensity',.false.)
          i_var = i_var +1
        endif
        if(out_cp) then
          call add_output_var(out_vars_w(i_var), cp, &
                  'Cp',.true.)
          call add_output_var(out_vars_vp(i_var), cp, &
                  'Cp',.true.)
          i_var = i_var +1
        endif
        if(out_surfvel) then
          call add_output_var(out_vars_w(i_var), surfvel, &
                  'Surface_Velocity',.true.)
          call add_output_var(out_vars_vp(i_var), surfvel, &
                  'Surface_Velocity',.true.)
          i_var = i_var +1
        endif
        if(out_vel) then
          call add_output_var(out_vars_w(i_var), vel, &
                  'Velocity',.true.)
          call add_output_var(out_vars_vp(i_var), vel, &
                  'Velocity',.true.)
          i_var = i_var +1
        endif
        if(out_press) then
          call add_output_var(out_vars_w(i_var), press, &
                  'Pressure',.true.)
          call add_output_var(out_vars_vp(i_var), press, &
                  'Pressure',.true.)
          i_var = i_var +1
        endif
        if(out_turbvisc) then
          call add_output_var(out_vars_w(i_var), vpturbvisc, &
                  'Turbulent_Viscosity',.true.)
          call add_output_var(out_vars_vp(i_var), vpturbvisc, &
                  'Turbulent_Viscosity',.false.)
          call add_output_var(out_vars(i_var), vpturbvisc, &
                  'Turbulent_Viscosity',.true.)
          i_var = i_var +1
        endif
        if(out_vort_vec) then
          call add_output_var(out_vars_vp(i_var), vpvort_v, &
                  'Vorticity',.false.)
          call add_output_var(out_vars_w(i_var), vpvort_v, &
                  'Vorticity',.true.)
          call add_output_var(out_vars(i_var), vpvort_v, &
                  ' Vorticity',.true.)
          i_var = i_var +1
        endif
        if(out_vrad) then
          call add_output_var(out_vars_vp(i_var), v_rad, &
                  'VortexRad',.false.)
          call add_output_var(out_vars_w(i_var), v_rad, &
                  'VortexRad',.true.)
          call add_output_var(out_vars(i_var), v_rad, &
                  'VortexRad',.true.)
          i_var = i_var +1
        endif
        
        !Output the results (with wake)
        select case (trim(out_frmt))
          case ('tecplot')
            filename = trim(filename)//'.plt'
            call  tec_out_viz(filename, t, &
                      points_exp, elems, out_vars, &
                      w_rr=wpoints, w_ee=welems, w_vars=out_vars_w, &
                      vp_rr=vppoints, vp_vars=out_vars_vp)
          case ('vtk')
            filename = trim(filename)//'.vtu'
            call  vtk_out_viz(filename, &
                        points_exp, elems, out_vars, &
                      w_rr=wpoints, w_ee=welems, w_vars=out_vars_w, &
                      vp_rr=vppoints, vp_vars=out_vars_vp, &
                      separate_wake = separate_wake)
          case default
            call error('dust_post','','Unknown format '//trim(out_frmt)//&
                      ' for visualization output')
        end select

        deallocate (wpoints, welems,  wvort)
        call clear_output_vars(out_vars_w)
        call clear_output_vars(out_vars_vp)

      else

        !Output the results (without wake)
        select case (trim(out_frmt))
          case ('tecplot')
            filename = trim(filename)//'.plt'
            call  tec_out_viz(filename, t, &
                          points_exp, elems, out_vars)
          case ('vtk')
            filename = trim(filename)//'.vtu'
            call  vtk_out_viz(filename, &
                          points_exp, elems, out_vars)
          case default
            call error('dust_post','','Unknown format '//trim(out_frmt)//&
                        ' for visualization output')
        end select

      endif !output wake

    endif !not average

    call close_hdf5_file(floc)

    deallocate(refs_R, refs_off)
    !deallocate(print_var_names, print_vars)
    call clear_output_vars(out_vars)

    if (allocated(vort   ) ) deallocate(vort )
    if (allocated(press  ) ) deallocate(press)
    if (allocated(surfvel) ) deallocate(surfvel)
    if (allocated(vel    ) ) deallocate(vel  )
    if (allocated(cp     ) ) deallocate(cp   )


  end do ! Time loop

  !Print the average
  if(average) then
    write(filename,'(A)') trim(basename)//'_'//trim(an_name)//'_ave'
    !Output the results (without wake)
    select case (trim(out_frmt))
      case ('tecplot')
        filename = trim(filename)//'.plt'
        call  tec_out_viz(filename, t, &
                    points_ave, elems, ave_out_vars)
      case ('vtk')
        filename = trim(filename)//'.vtu'
        call  vtk_out_viz(filename, &
                    points_ave, elems, ave_out_vars)
      case default
        call error('dust_post','','Unknown format '//trim(out_frmt)//&
                    ' for visualization output')
    end select
    call clear_output_vars(ave_out_vars)
    deallocate(ave_out_vars)
  endif

  deallocate(points)
  call destroy_elements(comps)
  deallocate(comps)
  deallocate(out_vars)
  if(out_wake) deallocate(out_vars_w, out_vars_vp)


  write(msg,'(A,I0,A)') nl//'++++++++++ Visualization done'//nl
  call printout(trim(msg))

end subroutine post_viz

! ----------------------------------------------------------------------

end module mod_post_viz
